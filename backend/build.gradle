plugins {
    id "java"
    id "idea"
    id "org.springframework.boot" version "3.1.1"
    id "io.spring.dependency-management" version "1.1.0"
    id "io.freefair.lombok" version "8.0.1"
    id "com.github.spotbugs" version "5.0.14"
    id "com.github.ben-manes.versions" version "0.47.0"
    id "org.owasp.dependencycheck" version "8.3.1"
    id "com.diffplug.spotless" version "6.19.0"
    id "info.solidsoft.pitest" version "1.9.11"
}

spotless {
    java {
        googleJavaFormat()
    }
}

ext {
    javaUuidGeneratorVersion = "4.2.0"
    logstashVersion = "7.4"
    caffeineVersion = "3.1.6"
    mockServerVersion = "5.15.0"
    archUnitVersion = "1.0.1"
    jUnit5PitestVersion = "1.2.0"
}

ext['snakeyaml.version'] = "2.0";

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "com.fasterxml.uuid:java-uuid-generator:${javaUuidGeneratorVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashVersion}"
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "com.tngtech.archunit:archunit-junit5:${archUnitVersion}"
    testImplementation "org.mock-server:mockserver-spring-test-listener:${mockServerVersion}"
    testImplementation "org.pitest:pitest-junit5-plugin:${jUnit5PitestVersion}"
}

group = "de.otto.platform"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tag {
    message fromMandatorySystemProperty('message')
}

import java.text.SimpleDateFormat

bootJar {
    manifest {
        attributes(
                'Implementation-Vendor': project.group,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.properties['user.name'],
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

apply from: "${rootDir}/gradle/test.gradle"
apply from: "${rootDir}/gradle/spotbugs.gradle"
apply from: "${rootDir}/gradle/dependencycheck.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/pmd.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/pitest.gradle"
